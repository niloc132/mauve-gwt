dnl Process this with autoconf to create configure
AC_INIT([mauve],[0.0])
AC_CONFIG_SRCDIR([gnu/testlet/Testlet.java])
AM_INIT_AUTOMAKE([tar-pax])

dnl Check path and file separator types
ACX_CHECK_PATHNAME_STYLE_DOS

dnl For EXEEXT.
AC_PROG_CC

dnl Check for which JVM should be tested, default to "java"
AC_ARG_WITH(vm,
[  --with-vm=TESTJVM  				Run the tests with TESTJVM],
TEST_JAVA="$with_vm", TEST_JAVA="java")
AC_SUBST(TEST_JAVA)


AC_ARG_WITH(emma,
[  --with-emma(=JARLOCATION)       Use emma, either unpacked in classpath 
folder or at the specified JARLOCATION],
EMMA="$with_emma", EMMA="yes")
if test "$EMMA" = "yes"
then
EMMA="_auto_detect_emma_"
fi
AC_SUBST(EMMA)

AC_ARG_WITH(ecj-jar,
[  --with-ecj-jar=JARLOCATION       Use the ecj jar found at JARLOCATION for 
auto-compilation],
ECJ_JAR="$with_ecj_jar", ECJ_JAR=yes)
if test "$ECJ_JAR" = "yes"
then
ECJ_JAR="/usr/share/java/eclipse-ecj.jar"
fi

AC_SUBST(ECJ_JAR)

dnl auto-compilation is disabled by default because it requires the
dnl --with-classpath-install-dir option to be used as well, and so
dnl by disabling it, the standard "./configure" setup has a better
dnl chance of producing meaningful results.  If it were enabled
dnl by default many tests would fail because the compiler wouldn't
dnl have the correct bootclasspath
AC_ARG_ENABLE(auto-compilation,
[  --enable-auto-compilation            Use ecj to compile tests on the fly],
AUTO_COMPILE="$enable_auto_compilation",AUTO_COMPILE="yes")
AC_SUBST(AUTO_COMPILE)


JAVA=${JAVA-java}
AC_SUBST(JAVA)
JAVAC=${JAVAC-javac}
AC_SUBST(JAVAC)

SRCDIR=`(cd $srcdir; pwd)`
AC_SUBST(SRCDIR)
BUILDDIR=`pwd`
AC_SUBST(BUILDDIR)

dnl Specify the tempdir.
AC_ARG_WITH(tmpdir,
changequote(<<,>>)
<<  --with-tmpdir=DIR                Put temporary files in DIR [/tmp]>>,
changequote([,])
TMPDIR="$with_tmpdir")

TMPDIR=${TMPDIR-/tmp}
AC_SUBST(TMPDIR)

dnl Specify a mail server host for socket testing.  This allows you to
dnl choose a different server if 'mx10.gnu.org' is blocked by your firewall
AC_ARG_WITH(mailhost,
changequote(<<,>>)
<<  --with-mailhost=hostname         Server for socket tests [mx10.gnu.org]>>,
changequote([,])
MAIL_HOST="$with_mailhost")

MAIL_HOST=${MAIL_HOST-mx10.gnu.org}
AC_SUBST(MAIL_HOST)

if test ! -d gnu; then
  mkdir gnu
fi
if test ! -d gnu/testlet; then
  mkdir gnu/testlet
fi

AC_OUTPUT(Makefile gnu/testlet/config.java)
